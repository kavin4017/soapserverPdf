plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.example'
version = '1.0.0'
sourceCompatibility = '21'

configurations {
    jaxb
}

dependencies {
    // SOAP Web Services
    implementation 'org.springframework.boot:spring-boot-starter-web-services'

    implementation 'wsdl4j:wsdl4j:1.6.3'

    // PDF Generation
    implementation 'org.apache.pdfbox:pdfbox:3.0.2'

    // Jakarta XML Binding
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.1'
    runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:4.0.2'

    // JAXB Tools
    jaxb 'org.glassfish.jaxb:jaxb-xjc:4.0.2'
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated/sources/jaxb"
    outputs.dir sourcesDir

    doLast {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath

            mkdir(dir: sourcesDir)

            xjc(destdir: sourcesDir, package: 'com.example.soap.model') {
                schema(dir: "src/main/resources/schema", includes: "request.xsd, requestNew.xsd")
                arg(value: "-wsdl")
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs += genJaxb.sourcesDir
        }
    }
}

compileJava.dependsOn genJaxb

bootJar {
    archiveFileName = 'provider.jar'
}

repositories {
    mavenCentral()
}
